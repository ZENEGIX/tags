group 'ru.zenegix.tags'
version '1.0-SNAPSHOT'

apply plugin: 'java'

sourceCompatibility = 1.8

allprojects{ project ->
    buildscript {
        repositories {
            mavenCentral()
            jcenter()
            maven {
                url "https://plugins.gradle.org/m2/"
            }
            maven {
                url "https://oss.sonatype.org/content/repositories/snapshots/"
            }
        }

        dependencies {
            classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
        }
    }

    repositories {
        mavenCentral()
        jcenter()
        mavenLocal()
        maven { // Spigot repo
            url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    build.dependsOn(sourcesJar)
    build.dependsOn(javadocJar)

    if (!project.name.equalsIgnoreCase("tags-example")) {
        apply plugin: 'maven-publish'

        publishing {
            publications {
                maven(MavenPublication) {
                    groupId project.group
                    artifactId project.name
                    version project.version
                    from components.java
                }
            }
            repositories {
                mavenLocal()

                if (project.hasProperty("publish_user") && project.hasProperty("publish_password")) {
                    maven {
                        if (project.version.endsWith('-SNAPSHOT')) {
                            url "https://repo.zenegix.ru/content/repositories/snapshots/"
                        } else {
                            url "https://repo.zenegix.ru/content/repositories/releases/"
                        }

                        credentials {
                            username "${publish_user}"
                            password "${publish_password}"
                        }
                    }
                }
            }
        }
    }
}
